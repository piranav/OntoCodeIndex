PREFIX laco: <https://example.org/laco#>
PREFIX next: <https://example.org/next#>
PREFIX lasa: <https://example.org/lasa#>
PREFIX dct:  <http://purl.org/dc/terms/>

# N001: Page component in app router
CONSTRUCT {
  ?u a next:Page ; next:segmentType "page" .
}
WHERE {
  ?u a laco:Callable ; laco:declaredIn ?f ; laco:isExportedDefault true .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/app/.+/page\\.(tsx|jsx)$", "i"))
}
;

# N002: Layout component
CONSTRUCT {
  ?u a next:Layout ; next:segmentType "layout" .
}
WHERE {
  ?u a laco:Callable ; laco:declaredIn ?f ; laco:isExportedDefault true .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/app/.+/layout\\.(tsx|jsx)$", "i"))
}
;

# N003: Middleware
CONSTRUCT {
  ?u a next:Middleware .
}
WHERE {
  ?u a laco:Callable ; laco:declaredIn ?f .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/middleware\\.(ts|js)$", "i"))
}
;

# N004: API Route in app router
CONSTRUCT {
  ?u a next:APIRoute ; next:segmentType "route" .
}
WHERE {
  ?u a laco:Callable ; laco:declaredIn ?f .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/app/.+/route\\.(ts|js|tsx|jsx)$", "i"))
}
;

# N005: API Route in pages router
CONSTRUCT {
  ?u a next:APIRoute ; next:segmentType "route" .
}
WHERE {
  ?u a laco:Callable ; laco:declaredIn ?f ; laco:isExportedDefault true .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/pages/api/.+\\.(ts|js|tsx|jsx)$", "i"))
}
;

# N010: routePattern for app router entrypoints
CONSTRUCT {
  ?u next:routePattern ?pattern .
}
WHERE {
  ?u a laco:Unit ; laco:declaredIn ?f .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/app/.+/(page|layout|route|default)\\.(tsx|jsx|ts|js)$", "i"))
  BIND(REPLACE(?p, "^.*?/app", "", "i") AS ?subpath)
  BIND(REPLACE(?subpath, "/(page|layout|route|default)\\.(tsx|jsx|ts|js)$", "", "i") AS ?trimmed)
  BIND(REPLACE(?trimmed, "/index$", "", "i") AS ?withoutIndex)
  BIND(REPLACE(REPLACE(REPLACE(?withoutIndex, "\\[\\[\\.\\.\\.([^/\\]]+)\\]\\]", "*$1"), "\\[\\.\\.\\.([^/\\]]+)\\]", "*$1"), "\\[([^/\\]]+)\\]", ":$1") AS ?withDynamics)
  BIND(REPLACE(?withDynamics, "/{2,}", "/") AS ?normalized)
  BIND(IF(?normalized = "", "/", CONCAT("/", REPLACE(?normalized, "^/", ""))) AS ?pattern)
}
;

# N011: routePattern for pages router API routes
CONSTRUCT {
  ?u next:routePattern ?pattern .
}
WHERE {
  ?u a laco:Unit ; laco:declaredIn ?f .
  ?f dct:path ?p .
  FILTER(REGEX(?p, "/pages/api/.+\\.(tsx|jsx|ts|js)$", "i"))
  BIND(REPLACE(?p, "^.*?/pages", "", "i") AS ?subpath)
  BIND(REPLACE(?subpath, "\\.(tsx|jsx|ts|js)$", "", "i") AS ?withoutExt)
  BIND(REPLACE(?withoutExt, "/index$", "", "i") AS ?trimmed)
  BIND(REPLACE(REPLACE(REPLACE(?trimmed, "\\[\\[\\.\\.\\.([^/\\]]+)\\]\\]", "*$1"), "\\[\\.\\.\\.([^/\\]]+)\\]", "*$1"), "\\[([^/\\]]+)\\]", ":$1") AS ?withDynamics)
  BIND(REPLACE(?withDynamics, "/{2,}", "/") AS ?normalized)
  BIND(IF(?normalized = "", "/", CONCAT("/", REPLACE(?normalized, "^/", ""))) AS ?pattern)
}
;

# N020: runtime from exported const runtime
CONSTRUCT {
  ?u next:runtime ?rt .
}
WHERE {
  ?u a next:APIRoute ; laco:declaredIn ?f .
  ?f laco:exports ?cfg .
  ?cfg laco:qualifiedName ?qn ; laco:hasType ?t .
  FILTER(?qn = "runtime")
  ?t dct:title ?rt . # "edge" or "node"
}
;

# N030: usesClient via top-of-file directive
CONSTRUCT {
  ?u next:usesClient true .
}
WHERE {
  ?u a next:Page ; laco:declaredIn ?f .
  ?f laco:exports ?u .
  ?f <https://example.org/laco/ts#hasUseClientDirective> true
}
